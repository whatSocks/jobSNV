= Using a Professional Social Network to Target Potential Hires
:neo4j-version: 2.1.0
:author: Your Name Here
:description: A sentence description.
:twitter: @yourTwitter,
:tags: domain:animals, use-case:social network 


== Table of Contents

* *<<the_network, Using a Job Search Social Network to Target Potential Hires>>*
** <<introduction, Job Networking Platforms, an Introduction>>
** <<this_graphgist, This GraphGist>>
* *<<targeted_ads, Targeted Employment Ads: A Closer Look>>*
** <<preview, Planning Group Recommendations>>
* *<<closer_look, A Closer Look at the Network>>*
** <<groups, Groups>>
** <<companies, Companies>>
* *<<problem_1, Finding the Best Group in which to Advertise>>*
** <<solution1, Finding Target Groups through Employees>>
** <<solution2, Finding Target Groups through Current Employees _with a Similar Skill Set_>>
** <<solution3, Finding Target Groups _with the Highest Number of Experts_>>
* *<<conclusion, Conclusion>>*
** <<why_neo, Why Neo4j?>>
* *<<use_case, Use Case: Viadeo>>*
* *<<references, References>>*


[[the_network]]
== Using a Professional Social Network to Target Potential Hires

[[introduction]]
=== Job Networking Platforms, an Introduction

For the majority of users, social networks--including professional networking sites--are free services used to connect with personal or professional contacts, join interest-specific groups, and network with potential employers, clients, or employees. To both make and sustain the magic of social networks and web 2.0, social network owners have to not only provide their users and potential customers with fast, powerful analytics, but be able to sustain themselves while providing value to their shareholders. 

This GrapGist explores how a social network provider, may use a graph database to both provide its customers and users (should they be separate groups) with value while monetizing their service. In particular, it explores how a theoretical professional social network owner might provide high-value recommendations for groups in which a company might place job adverts.

The companies operating the social networks use various means to monetize their service, including sidebar ads from users and third parties, premium accounts that are free from these ads, and charging for presenting ads in the newsfeeds of particular users. 

Companies and individuals purchasing services like advertisements from social network sites want the best bang for their buck: they are paying a premium to present their ad to potential clients, and they would rather not 'waste' views on users that have no interest in the product in question. Social networking services use a variety of means to offer their customers the best access to high-value targets. For example, social networks offer targeted ads based on combinations of: 

==== User Interests

If someone searches for 'butterflies' on Amazon.com while logged into Facebook, she is then presented with lepidopterae-related ads across Facebook

==== Social Network

Companies on Facebook can target ads at the friends of particular users, as these users presumably share relevant characteristics with the initial users, such as location and interests

[[this_graphgist]]
=== This GraphGist 

This GraphGist explores targeted 'marketing' on job networking sites such as LinkedIn, Vieadeo, and Xing using a combination of the techniques above. Our toy professional networking site allows users and companies to use the site for free, but does charge for job posting, and offers several options on how to put the job posting in front of the most relevant (and hopefully qualified and interested) users. 

==== A brief description of our professional network:

Our toy netowork includes: 

- *Persons*: Persons, or the free users of the site, can be actively looking for work, happily employed and using the site to connect with fellow professionals, or a combination. Persons can make connections between themeselves and other Persons, the Company they work for, Groups they are a member of, Jobs they can express interest in, and Skills they are interested in or currently use.

- *Companies*: Companies in this example are interested in hiring the best talent, and are willing to pay to present job postings (Jobs) to targeted potential employees. 

- *Jobs*: Jobs are posted by Companies, connected to by Persons, and associated with Skills

- *Skills*: Associated with Persons and Jobs, Skills are used by Companies to find relelvant potential employees and by Persons to find relevant Jobs and Groups.

- *Groups*: Although Persons can form connections with other Persons that they know, Persons can join Groups based on common interests, which may or may not include Skills, previous employers, educational experiences, or other shared characteristics. 

==== Some loose ends:

Since this is an example social network, we had to make the following abstractions:

- There's no time but the present. The network ignores past Person-Job relationships and assumes all job postings, group membership, and social connections are current
- Multiple people can have the same job, but jobs can only belong to one company
- Aside from id and a name, nodes don't have other traits, emphasizing the power of the connections and relationships

[[targeted_ads]]
== Targeted Employment Ads: A Closer Look

From the point of view of the Companies participating in the Jobs posting market, paying for job ads is costly and often inefficient. Not only does the posting cost money, the Company has to process and filter applicants, raising the cost of ineffective (or no) targeting. In short, Companies are willing to pay a premium for higher precision. Our toy network wants to offer Companies the option to advertise job postings in particular Groups, and will recommend the best Groups for a particular Company's hiring needs. 

Why not take the naive approach and look for groups with the highest proportion of members with target skills? Job networking sites serve groups with related but sometimes opposing interests. Users want to showcase themselves as ideal candidates, and Companies want to be able to find actually valuable candidates using limited information. 

Although users may not explicitly lie about their experience, they may slightly misrepresent their skills and abilities to mirror that of a more or less experienced person. In addition, users may also join Skill-specific groups due to general interest in learning about the Skill, because they want to network, or for a variety of other reasons. 

[[preview]]
=== Planning Group Recommendations

We can take several paths to generate group recommendations for Companies, which will be described later in this document:

- Find groups with relevant employees as members
- Find groups with relevant high-value users ("experts") as members

For more information on how to find experts in a network, take a look at 'http://gist.neo4j.org/?cc121734efe6a7acfdae[Aardvark: The Anatomy of a Large-Scale Social Search Engine]'

//hide
//setup
[source,cypher]
----
//create the Person nodes
CREATE (`1`:Person {id:1, name:'Abeer Fathy'}), (`2`:Person {id:2, name:'An Yu-bao'}), (`3`:Person {id:3, name:'Anastasiya Vasilyev'}), (`4`:Person {id:4, name:'Anna Zaytesev'}), (`5`:Person {id:5, name:'Asley Leger'}), 
(`6`:Person {id:6, name:'Aziza Hsuang-tsung'}), (`7`:Person {id:7, name:'Bai Vasilyev'}), (`8`:Person {id:8, name:'Barbra Schon'}), (`9`:Person {id:9, name:'Bart Kosana'}), (`10`:Person {id:10, name:'Bill Hinzman'}), 
(`11`:Person {id:11, name:'Bill Cardille'}), (`12`:Person {id:12, name:'Bobba Fett'}), (`13`:Person {id:13, name:'Cai Shen'}), (`14`:Person {id:14, name:'Cammy Schott'}), (`15`:Person {id:15, name:'Cammy Vinogradov'}), (`16`:Person {id:16, name:'Cammy Sokolov'}), (`17`:Person {id:17, name:'Chan Morozov'}), (`18`:Person {id:18, name:'Chan Bang'}), (`19`:Person {id:19, name:'Charles Craig'}), (`20`:Person {id:20, name:'Chew Kwan-yew'}), 
(`21`:Person {id:21, name:'Chiang Popov'}), (`22`:Person {id:22, name:'Chiu Xiao-yong'}), (`23`:Person {id:23, name:'Dai Lu-fang'}), (`24`:Person {id:24, name:'Dariya Solovynov'}), (`25`:Person {id:25, name:'Dariya Semyonov'}), (`26`:Person {id:26, name:'Dariya Vorobyrov'}), (`27`:Person {id:27, name:'David Cardille'}), (`28`:Person {id:28, name:'Deeanna Lacroix'}), (`29`:Person {id:29, name:'Deng Szeto'}), (`30`:Person {id:30, name:'Dina Schott'})

//create the Company nodes
CREATE (`company1`:Company {id:1, name:'CRC Press'}), (`company2`:Company {id:2, name:'Quantum Stabilzers'}), (`company3`:Company {id:3, name:'The SilverLogic'})

//create the Job nodes
CREATE (`job1`:Job {id:1, name:'Designer'}), (`job2`:Job {id:2, name:'Naval Eng.'}), (`job3`:Job {id:3, name:'Py Dev'})

//create the Skill nodes for the Python Developer Job
CREATE (`skill1`:Skill {id:1, name:'Django'}), (`skill2`:Skill {id:2, name:'Flask'}), (`skill3`:Skill {id:3, name:'Git'})

//create the Skill nodes for the Naval Engineer Job
CREATE (`skill4`:Skill {id:4, name:'CAD'}), (`skill5`:Skill {id:5, name:'Excel'}), (`skill6`:Skill {id:6, name:'Project Management'})

//create the Skill nodes for the Graphic Designer Job
CREATE (`skill7`:Skill {id:7, name:'Illustrator'}),(`skill8`:Skill {id:8, name:'InDesign'}), (`skill9`:Skill {id:9, name:'Photoshop'})

//create Job-Skill relationships
CREATE (`job1`)-[:HAS_SKILL]->(`skill7`), (`job1`)-[:HAS_SKILL]->(`skill8`),(`job1`)-[:HAS_SKILL]->(`skill9`),
	(`job2`)-[:HAS_SKILL]->(`skill4`), (`job2`)-[:HAS_SKILL]->(`skill5`),(`job2`)-[:HAS_SKILL]->(`skill6`),
	(`job3`)-[:HAS_SKILL]->(`skill1`),(`job3`)-[:HAS_SKILL]->(`skill2`)

//create the job-company relationshp
CREATE (`job1`)-[:JOB_AT]->(`company3`), (`job2`)-[:JOB_AT]->(`company2`), (`job3`)-[:JOB_AT]->(`company3`)

//create the Group nodes
CREATE (`group1`:Group {id:1, name:'Python Lovers'}), (`group2`:Group {id:2, name:'MIA Alums'}), (`group3`:Group {id:3, name:'Snakes Forever'})

CREATE (`group4`:Group {id:4, name:'Yachties Unite'}), (`group5`:Group {id:5, name:'Excel Support Group'}), (`group6`:Group {id:6, name:'Fishing is Life'})

CREATE(`group7`:Group {id:7, name:'Marketing Pros'}),(`group8`:Group {id:8, name:'Python Users Unite'}),(`group9`:Group {id:9, name:'Learn Python the Hard Way'})

//create the Person-Person relationships
CREATE (`1`)-[:KNOWS]->(`2`), (`1`)-[:KNOWS]->(`3`), (`1`)-[:KNOWS]->(`4`), (`1`)-[:KNOWS]->(`5`), 
	(`2`)-[:KNOWS]->(`3`), (`2`)-[:KNOWS]->(`4`), (`2`)-[:KNOWS]->(`5`), 
	(`3`)-[:KNOWS]->(`4`), (`3`)-[:KNOWS]->(`5`), 
	(`4`)-[:KNOWS]->(`5`), 
	(`5`)-[:KNOWS]->(`7`), 
	(`6`)-[:KNOWS]->(`7`), 
	(`8`)-[:KNOWS]->(`7`), 
	(`9`)-[:KNOWS]->(`3`), (`9`)-[:KNOWS]->(`5`), 
	(`10`)-[:KNOWS]->(`2`), (`10`)-[:KNOWS]->(`4`), (`10`)-[:KNOWS]->(`6`), 
	(`11`)-[:KNOWS]->(`27`), (`11`)-[:KNOWS]->(`1`), (`11`)-[:KNOWS]->(`3`), (`11`)-[:KNOWS]->(`5`), 
	(`12`)-[:KNOWS]->(`4`), (`12`)-[:KNOWS]->(`6`), 
	(`13`)-[:KNOWS]->(`13`), (`13`)-[:KNOWS]->(`1`), 
	(`14`)-[:KNOWS]->(`30`), (`14`)-[:KNOWS]->(`4`), 
	(`15`)-[:KNOWS]->(`30`), (`15`)-[:KNOWS]->(`26`), (`15`)-[:KNOWS]->(`21`), (`15`)-[:KNOWS]->(`3`), (`15`)-[:KNOWS]->(`5`), 
	(`16`)-[:KNOWS]->(`11`), (`16`)-[:KNOWS]->(`4`), 
	(`17`)-[:KNOWS]->(`17`), (`17`)-[:KNOWS]->(`28`), 
	(`18`)-[:KNOWS]->(`12`), (`18`)-[:KNOWS]->(`2`), 
	(`19`)-[:KNOWS]->(`3`), (`19`)-[:KNOWS]->(`5`), 
	(`20`)-[:KNOWS]->(`1`), (`20`)-[:KNOWS]->(`2`), 
	(`21`)-[:KNOWS]->(`1`), (`21`)-[:KNOWS]->(`3`), (`21`)-[:KNOWS]->(`5`), 
	(`22`)-[:KNOWS]->(`4`), (`22`)-[:KNOWS]->(`6`), (`22`)-[:KNOWS]->(`2`), 
	(`24`)-[:KNOWS]->(`7`), (`24`)-[:KNOWS]->(`4`), 
	(`25`)-[:KNOWS]->(`3`), (`25`)-[:KNOWS]->(`5`), 
	(`26`)-[:KNOWS]->(`18`), (`26`)-[:KNOWS]->(`2`), (`26`)-[:KNOWS]->(`4`), (`26`)-[:KNOWS]->(`6`), 
	(`27`)-[:KNOWS]->(`11`), (`27`)-[:KNOWS]->(`1`), (`27`)-[:KNOWS]->(`3`), (`27`)-[:KNOWS]->(`5`), 
	(`28`)-[:KNOWS]->(`4`), 
	(`29`)-[:KNOWS]->(`19`), (`29`)-[:KNOWS]->(`1`), 
	(`30`)-[:KNOWS]->(`14`)

//Create the Person-Group relationships

//Groups of interest to Graphic Designers
CREATE (`1`)-[:MEMBER]->(`group1`), (`1`)-[:MEMBER]->(`group2`),(`2`)-[:MEMBER]->(`group1`), (`3`)-[:MEMBER]->(`group1`), (`4`)-[:MEMBER]->(`group1`)
CREATE (`5`)-[:MEMBER]->(`group2`), (`6`)-[:MEMBER]->(`group2`), (`7`)-[:MEMBER]->(`group2`), (`8`)-[:MEMBER]->(`group2`), (`9`)-[:MEMBER]->(`group2`)
CREATE (`1`)-[:MEMBER]->(`group3`), (`3`)-[:MEMBER]->(`group3`), (`5`)-[:MEMBER]->(`group3`), (`7`)-[:MEMBER]->(`group3`), (`9`)-[:MEMBER]->(`group3`)

//Groups of interest to Naval Engineers
CREATE (`11`)-[:MEMBER]->(`group4`), (`12`)-[:MEMBER]->(`group4`), (`13`)-[:MEMBER]->(`group4`), (`14`)-[:MEMBER]->(`group4`)
CREATE (`15`)-[:MEMBER]->(`group4`), (`16`)-[:MEMBER]->(`group4`), (`17`)-[:MEMBER]->(`group4`), (`18`)-[:MEMBER]->(`group5`), (`19`)-[:MEMBER]->(`group5`)
CREATE (`11`)-[:MEMBER]->(`group5`), (`13`)-[:MEMBER]->(`group5`), (`15`)-[:MEMBER]->(`group5`), (`17`)-[:MEMBER]->(`group5`), (`19`)-[:MEMBER]->(`group5`)

//Groups of interest to Python Engineers
CREATE (`21`)-[:MEMBER]->(`group6`), (`22`)-[:MEMBER]->(`group6`), (`23`)-[:MEMBER]->(`group6`), (`24`)-[:MEMBER]->(`group6`)
CREATE (`25`)-[:MEMBER]->(`group7`), (`26`)-[:MEMBER]->(`group7`)
CREATE (`21`)-[:MEMBER]->(`group9`), (`23`)-[:MEMBER]->(`group9`), (`25`)-[:MEMBER]->(`group9`), (`27`)-[:MEMBER]->(`group5`), (`29`)-[:MEMBER]->(`group5`)

//create the Person-Skill relationships
CREATE (`1`)-[:HAS_SKILL]->(`skill1`), (`1`)-[:HAS_SKILL]->(`skill2`), (`1`)-[:HAS_SKILL]->(`skill3`), (`1`)-[:HAS_SKILL]->(`skill4`), 
	(`2`)-[:HAS_SKILL]->(`skill1`), (`2`)-[:HAS_SKILL]->(`skill2`), (`2`)-[:HAS_SKILL]->(`skill3`), 
	(`3`)-[:HAS_SKILL]->(`skill1`), (`3`)-[:HAS_SKILL]->(`skill2`), 
	(`4`)-[:HAS_SKILL]->(`skill1`), 
	(`5`)-[:HAS_SKILL]->(`skill1`), 
	(`6`)-[:HAS_SKILL]->(`skill1`), 
	(`8`)-[:HAS_SKILL]->(`skill1`), 
	(`9`)-[:HAS_SKILL]->(`skill1`), (`9`)-[:HAS_SKILL]->(`skill2`), 
	(`10`)-[:HAS_SKILL]->(`skill1`), (`10`)-[:HAS_SKILL]->(`skill2`), (`10`)-[:HAS_SKILL]->(`skill3`), 
	(`11`)-[:HAS_SKILL]->(`skill4`), (`11`)-[:HAS_SKILL]->(`skill5`), (`11`)-[:HAS_SKILL]->(`skill6`), (`11`)-[:HAS_SKILL]->(`skill7`), 
	(`12`)-[:HAS_SKILL]->(`skill4`), (`12`)-[:HAS_SKILL]->(`skill5`), 
	(`13`)-[:HAS_SKILL]->(`skill4`), (`13`)-[:HAS_SKILL]->(`skill5`), 
	(`14`)-[:HAS_SKILL]->(`skill4`), (`14`)-[:HAS_SKILL]->(`skill5`), 
	(`15`)-[:HAS_SKILL]->(`skill4`), (`15`)-[:HAS_SKILL]->(`skill5`), (`15`)-[:HAS_SKILL]->(`skill6`), (`15`)-[:HAS_SKILL]->(`skill7`), (`15`)-[:HAS_SKILL]->(`skill8`), 
	(`16`)-[:HAS_SKILL]->(`skill4`), (`16`)-[:HAS_SKILL]->(`skill5`), 
	(`17`)-[:HAS_SKILL]->(`skill4`), (`17`)-[:HAS_SKILL]->(`skill5`), 
	(`18`)-[:HAS_SKILL]->(`skill4`), (`18`)-[:HAS_SKILL]->(`skill5`), 
	(`19`)-[:HAS_SKILL]->(`skill4`), (`19`)-[:HAS_SKILL]->(`skill5`), 
	(`20`)-[:HAS_SKILL]->(`skill7`), (`20`)-[:HAS_SKILL]->(`skill8`), 
	(`21`)-[:HAS_SKILL]->(`skill7`), (`21`)-[:HAS_SKILL]->(`skill8`), (`21`)-[:HAS_SKILL]->(`skill9`), 
	(`22`)-[:HAS_SKILL]->(`skill7`), (`22`)-[:HAS_SKILL]->(`skill8`), (`22`)-[:HAS_SKILL]->(`skill9`), 
	(`24`)-[:HAS_SKILL]->(`skill7`), (`24`)-[:HAS_SKILL]->(`skill8`), 
	(`25`)-[:HAS_SKILL]->(`skill7`), (`25`)-[:HAS_SKILL]->(`skill8`), 
	(`26`)-[:HAS_SKILL]->(`skill7`), (`26`)-[:HAS_SKILL]->(`skill8`), (`26`)-[:HAS_SKILL]->(`skill9`), 
	(`27`)-[:HAS_SKILL]->(`skill7`), (`27`)-[:HAS_SKILL]->(`skill8`), (`27`)-[:HAS_SKILL]->(`skill9`), 
	(`28`)-[:HAS_SKILL]->(`skill4`), 
	(`29`)-[:HAS_SKILL]->(`skill7`), (`29`)-[:HAS_SKILL]->(`skill8`), 
	(`30`)-[:HAS_SKILL]->(`skill7`)

//create the user-company relationships
CREATE (`1`)-[:WORKS_AT]->(`company1`), (`2`)-[:WORKS_AT]->(`company2`), (`3`)-[:WORKS_AT]->(`company3`), 
(`4`)-[:WORKS_AT]->(`company1`), (`5`)-[:WORKS_AT]->(`company2`), (`6`)-[:WORKS_AT]->(`company3`), (`7`)-[:WORKS_AT]->(`company1`), (`8`)-[:WORKS_AT]->(`company2`), (`9`)-[:WORKS_AT]->(`company3`), (`10`)-[:WORKS_AT]->(`company1`), (`11`)-[:WORKS_AT]->(`company2`), (`12`)-[:WORKS_AT]->(`company3`), (`13`)-[:WORKS_AT]->(`company1`), (`14`)-[:WORKS_AT]->(`company2`), (`15`)-[:WORKS_AT]->(`company3`), (`16`)-[:WORKS_AT]->(`company1`), (`17`)-[:WORKS_AT]->(`company2`), (`18`)-[:WORKS_AT]->(`company3`), (`19`)-[:WORKS_AT]->(`company1`), (`20`)-[:WORKS_AT]->(`company2`)

RETURN *
LIMIT 50
----
// graph_result

[[closer_look]]
== A Closer Look at the Network

[[groups]]
=== Groups, their Members, and their Skills

[source,cypher]
----
MATCH (s:Skill)-[:HAS_SKILL]-(p:Person)-[:MEMBER]-(g:Group)
RETURN g.name AS Group, s.name AS Skill, count(DISTINCT p) AS `Members`
ORDER BY Skill ASC, Members DESC
----
//table

If a company were interested in posting an ad for and hiring Django developers, there are several groups competing for the pleasure of an advertisement offering potential financial security. If a Company has enough resources to advertise a Job to one group, which group should it advertise to? There are various paths by which to choose which group. 

[[companies]]
=== Companies, their Employees, and their Skills

[source,cypher]
----
MATCH (c:Company)-[:WORKS_AT]-(p:Person)-[:HAS_SKILL]-(s:Skill)
RETURN c.name AS Company, count(DISTINCT p) AS `Employees`, s.name AS `Has Skill`
ORDER BY Company ASC, Employees DESC
----
//table

Assuming the Companies in our network are looking for employees with similar skillsets and social fit as their current employees, a Company might prefer a job candidate with more connections to current employees over one with more connections to experts in a particular Skill, depending on the position and how easy required Skills are to learn. 

[[jobs]]
=== Companies, their Open Jobs, and their Skills

[source,cypher]
----
MATCH (c:Company)-[:JOB_AT]-(j:Job)-[:HAS_SKILL]-(s:Skill)
RETURN c.name AS Company, j.name AS `Jobs`, s.name AS `Skills Required`
ORDER BY Company ASC, Jobs DESC
----
//table

Continuing the example from above, notice that the Company _The SilverLogic_ has an open job for a Python Developer that requires the skills Django and Flask. Let's see which group might be the best to advertise this open position.   

[[problem_1]]
== Finding the Best Group in which to Advertise

[[solution1]]
=== Finding Target Groups through Employees 

[source,cypher]
----
MATCH (p:Person)-[:WORKS_AT]-(c:Company), (p)-[:MEMBER]-(g:Group)
RETURN DISTINCT c.name AS Company, g.name AS `Target Groups`, count(DISTINCT p) AS `Count of Member Employees`
ORDER BY Company, `Count of Member Employees` DESC
----
//table

Assuming Companies are looking for a low-skill worker, like a high-school intern, that will work well with current employees, a good strategy might be to simply look for groups with the highest number of current employees. 

[[solution2]]
=== Finding Target Groups through Current Employees _with a Similar Skill Set as an Open Job_

[source,cypher]
----
MATCH (c:Company)-[:JOB_AT]-(j:Job)-[:HAS_SKILL]-(s:Skill),(s)-[:HAS_SKILL]-(p:Person)-[:WORKS_AT]-(c),(p)-[:MEMBER]-(g:Group)
RETURN DISTINCT c.name AS Company, j.name AS Job, s.name AS `Skills Required`, g.name AS Group, count(DISTINCT p) AS `Count of Member Employees`
ORDER BY  Company,`Count of Member Employees` DESC
----
//table

Assuming Companies are looking for skilled workers that will work well with current employees, filtering for job-specific skills in addition to social connections may render useful results. 

_description of results here_

[[solution3]]
=== Finding Target Groups _with the Highest Number of Experts_

Although going through current employees is generally positive, what if a Company has no contacts in a particular skill set? 

First, find an expert, as defined by someone with lots people in his or her network with the same skills. 

[source,cypher]
----
MATCH (s:Skill)-[:HAS_SKILL]-(expert:Person)-[:KNOWS]-(a:Person)-[:HAS_SKILL]-(s)
RETURN DISTINCT s.name AS Skill, count(DISTINCT expert) AS `Count of Experts`
ORDER BY Skill DESC, `Count of Experts` DESC
----
//table

What groups are the better-connected experts in? Are these groups different from those of the less-connected experts?

[source,cypher]
----
MATCH (s:Skill)-[:HAS_SKILL]-(expert:Person)-[:KNOWS]-(a:Person)-[:HAS_SKILL]-(s),(expert)-[:MEMBER]-(g:Group)
RETURN DISTINCT g.name as Group, s.name AS Skill, count(DISTINCT expert) AS `Count of Experts`
ORDER BY Skill DESC, `Count of Experts` DESC
----
//table

_description of results here_


[[conclusion]]
== Conclusion

_description of results here_


[[why_neo]]
=== Why Neo4j?

image:https://dl.dropboxusercontent.com/u/14493611/neo4j-logo.png[Neo4j Logo]

Although Cypher has extraordinary features that might immediately improve these Group recommendations, using these very simple and related searches can produce practical results across a variety of analytical needs. 

[[use_case]]
== Use Case: Viadeo

When Viadeo, a professional networking service, expanded to a level unmanagable by their traditional MySQL database, the company experienced performance and storage issues that would not perform at the rate the company was growing. Looking for a scalable solution, Christophe Thibault, CTO, decided to implement Neo4j to store Viadeo’s data. Chosen for its performance, ease of use, and its ability to quickly integrate with other applications, Neo4j increased Viadeo’s performance by requiring less storage space and less time to restructure the graph.

[[references]]
== References

- 'http://www.neotechnology.com/viadeo-usecase/[Viadeo competes with LinkedIn while using Neo4j as its modern database]'
- 'http://en.wikipedia.org/wiki/Samuel_Johnson[Networks, Crowds, and Markets]'
- 'http://gist.neo4j.org/?cc121734efe6a7acfdae[Aardvark: The Anatomy of a Large-Scale Social Search Engine]'
